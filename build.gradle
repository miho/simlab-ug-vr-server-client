plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.google.protobuf' version '0.9.4'
    id 'jpro-gradle-plugin'
}

group = 'com.simlab'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

def grpcVersion = '1.60.0'
def protobufVersion = '3.25.1'

dependencies {
    // gRPC
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    
    // Lua parser for script analysis
//    implementation 'org.luaj:luaj-jse:3.0.1'
    
    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    
    // JSON for configuration
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // For protobuf compilation
    compileOnly "io.grpc:grpc-protobuf:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

application {
    mainClass = 'com.simlab.ug.client.ClientApplication'
}

// 
task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.simlab.ug.client.ClientApplication'
    doFirst {
        jvmArgs = ['--module-path', sourceSets.main.runtimeClasspath.asPath,
                   '--add-modules', 'javafx.controls,javafx.fxml']
    }
}

// 
task runServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.simlab.ug.server.ServerApplication'
    doFirst {
        jvmArgs = ['--module-path', sourceSets.main.runtimeClasspath.asPath,
                   '--add-modules', 'javafx.controls,javafx.fxml']
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',
                       'build/generated/source/proto/main/grpc',
                       'build/generated/source/proto/main/java']
        }
    }
}

jpro {
    // jpro server port
    port = 8080
}